buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'maven'

repositories {
	maven {
		name = 'SevenCommons'
		url = "http://maven.take-weiland.de"
	}
}

dependencies {
    compile 'de.take_weiland.mods.commons:SevenCommons:0.1.74:deobf'
}

repositories {
    maven {
        name "xcompwiz"
        url "http://maven.xcompwiz.com"
    }
}
dependencies {
    compile "com.xcompwiz.lookingglass:lookingglass:0.2.0.00:dev"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

version = "3.0a"

def ENV = System.getenv()
if (ENV.DRONE_BUILD_NUMBER) {
	version += "." + ENV.DRONE_BUILD_NUMBER
} else {
	version += ".local"
}

minecraft {
	version = "1.7.10-10.13.3.1370-1.7.10"
	runDir = "eclipse"
	
	replaceIn "cameracraft/CameraCraft.java"
	replace "@VERSION@", project.version

	mappings = "stable_12"
}

group = "de.take_weiland.mods.cameracraft" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "CameraCraft"

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'src'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

tasks.build.dependsOn('sourceJar', 'deobfJar')

processResources {
    from (sourceSets.main.resources.srcDirs)
}

artifacts {
	archives deobfJar 
	archives sourceJar
	archives jar
}

configurations {
    deployerJars
}

repositories {
    mavenCentral()
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
}

if (ENV.CC_MAVEN_URL) {
	uploadArchives {
		repositories.mavenDeployer {
			configuration = configurations.deployerJars
			repository(url: ENV.CC_MAVEN_URL) {
				authentication(userName: ENV.CC_MAVEN_USER, password: ENV.CC_MAVEN_PASS)
			}
		}
	}
}

idea.module.inheritOutputDirs = true